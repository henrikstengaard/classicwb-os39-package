;ClassicWB Startup-Sequence
;--------------------------
;OS39
;Amiga OS 3.2

; Execute Amiga OS 3.2 startup, if it exists
IF EXISTS "S:Amiga-OS-32-Startup"
  Execute "S:Amiga-OS-32-Startup"
ENDIF

; set failat to 21
FailAt 21

If Exists C:LoadModule
  ; load scsi.device, if it exists
  IF EXISTS DEVS:scsi.device
    ; load scsi.device, if current scsi.device is less than v43.45
    Version >NIL: "scsi.device" 43 45
    IF WARN
      C:LoadModule DEVS:scsi.device
    ENDIF
  ENDIF
EndIf

;Start OS3.9 Checks
IF EXISTS C:SquirrelSCSI
    C:SquirrelSCSI
EndIF
Set SCSIUpdate 1
IF EXISTS C:IDEFix
    C:IDEFix
    Set SCSIUpdate 0
EndIF
IF EXISTS SYS:Expansion/XSurfIDE
    Set SCSIUpdate 0
EndIF
IF EXISTS SYS:Prefs/Env-Archive/NOSCSIUPDATE
    Set SCSIUpdate 0
EndIf
IF $SCSIUpdate EQ 1
    C:SetPatch QUIET
ELSE
    C:SetPatch SKIPROMUPDATES "scsi.device" QUIET
EndIF
Unset SCSIUpdate
;End OS3.9 Checks 
C:CPU CHECKINSTALL

C:GetMouseInput LOCAL

IF $MouseInput EQ 1
  Unset MouseInput
  C:Assign >NIL: ENV: RAM:
  C:Assign >NIL: T: RAM:
  C:Execute S:Maxmem-Sequence
  EndCLI >NIL:
EndIF

IF $MouseInput EQ 2
  Unset MouseInput
  C:Execute S:Bootmenu-Sequence
  EndCLI >NIL:
EndIF

Unset MouseInput

IF EXISTS C:StackAttack
  C:StackAttack
EndIF
 
C:Version >NIL:
FailAt 21

IF EXISTS SYS:WBstartupD
  C:Rename >NIL: SYS:WBStartup SYS:WBStartupM
  C:Rename >NIL: SYS:WBStartupD SYS:WBStartup
EndIF

Resident >NIL: C:IconX PURE
Resident >NIL: C:WBRun PURE
Resident >NIL: C:Assign PURE
Resident >NIL: C:Execute PURE
Resident >NIL: C:SetEnv PURE REPLACE

IF EXISTS L:env-handler
  C:MakeDir RAM:T RAM:Clipboards
  L:env-handler
ELSE
  C:MakeDir RAM:T RAM:Clipboards RAM:ENV RAM:ENV/Sys
  C:Copy >NIL: ENVARC: RAM:ENV ALL NOREQ
  Assign >NIL: ENV: RAM:ENV
EndIF

IF EXISTS C:PatchRAM
  C:PatchRAM
EndIF

IF EXISTS C:TagLiFE
  C:TagLiFE
EndIF

Assign >NIL: T: RAM:T
Assign >NIL: CLIPS: RAM:Clipboards
Assign >NIL: REXX: S:
Assign >NIL: PRINTERS: DEVS:Printers
Assign >NIL: KEYMAPS: DEVS:Keymaps
Assign >NIL: LOCALE: SYS:Locale
Assign >NIL: LIBS: SYS:Classes ADD
Assign >NIL: HELP: LOCALE:Help DEFER

IF EXISTS C:BlazeWCP
  C:BlazeWCP
EndIF

IF EXISTS SYS:System/FBlit/FBlit
  SYS:System/FBlit/FBlit
  SYS:System/FBlit/FText
EndIF

BindDrivers
C:Mount >NIL: DEVS:DOSDrivers/~(#?.info)

SetEnv Language "english"
SetEnv Workbench $Workbench
SetEnv Kickstart $Kickstart
UnSet Workbench
UnSet Kickstart

IF EXISTS C:CyberBugFix
  C:CyberBugFix
EndIF

IF EXISTS C:IconBeFast
  C:IconBeFast 0
EndIF
 
C:AddDataTypes REFRESH QUIET
IF EXISTS SYS:System/VPrefs/VisualPrefs
  Run > NIL: SYS:System/VPrefs/VisualPrefs
EndIF
C:IPrefs
SYS:System/FPalette/FPPrefs
C:ConClip

Path >NIL: RAM: C: SYS:Utilities SYS:Rexxc SYS:System S: SYS:Prefs SYS:WBStartup SYS:Tools SYS:Tools/Commodities

SYS:System/REXXMast >NIL:

IF EXISTS S:User-Startup
  Execute S:User-Startup
EndIF

Resident Execute REMOVE
Resident Assign REMOVE

IF EXISTS C:StackAttack
  C:StackAttack AUTO
EndIF

C:LoadWB

EndCLI >NIL:
