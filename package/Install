; ClassicWB OS39 v28 Package Install Script
; -----------------------------------------
;
; Author: Henrik Noerfjand Stengaard 
; Date: 2021-12-16
;
; This script extracts and installs ClassicWB OS39 v28 created by Bloodwych from EAB. 
; With permission from Bloodwych it's been converted to a package for HstWB Installer.
; Original version of ClassicWB OS39 can be downloaded from http://classicwb.abime.net/.
; The install script is based on S/Startup-Sequence from System.hdf with following changes:
; - Copying Amiga OS 3.9 files from HD0: to HD1: has been replaced by extract ClassicWB OS39 on top of Amiga OS 3.9 installation.
; - Paths has been changed: SYS: to SYSTEMDIR:, C: to SYSTEMDIR:C/, L: to SYSTEMDIR:L/.
; - Modified versions of Temp enable and disable option scripts with changed paths.
; - Removed all "press enter to continue" expect last one used after installation complete message is shown.
; - Removed and reduced waits.
; - Adjusted text spacing.
; - Creates backup of startup sequence as "S:Startup-Sequence.BAK".
; - Creates backup of user startup as "S:User-Startup.BAK". 
; - Creates backup of original ClassicWB OS39 startup sequence as "S:Startup-Sequence.CWB".
; - Creates backup of original ClassicWB OS39 user startup as "S:User-Startup.CWB". 
; - Patch startup sequence and user startup with ClassicWB OS39 changes for best Amiga OS compatibility with existing and future versions.
; - Updated User-Startup to replace CON with KCON (KingCON).
; - Added support for Amiga OS 3.2, 3.1.4 and 3.1:
;   - Reinstall MUI to fix iGame.
;   - Added WBDock as replacement to AmiDock.
;   - Disabled and removed MyClock, if Amiga OS 3.2, 3.1.4 or 3.1 is installed.
;   - Delete WBStartup's not present, if Amiga OS 3.2, 3.1.4 or 3.1 is installed.
;   - Added NewIcons and ClassAct to remove icon borders and add icon transparency, if Amiga OS 3.1 is installed.
;   - Updated User-Startup to replace CON with KCON.
;   - Updated DirectoryOpus to use CON instead of VNC.
;   - Added WBRun to fix run menus.
;   - Disable Copper, BorderBlank, StackAttack, CyberBugFix, MyClock.


; Copy readme and screenshots to locale help for ClassicWB OS39
IF NOT EXISTS SYSTEMDIR:Locale/Help/ClassicWB-OS39
  makepath >NIL: SYSTEMDIR:Locale/Help/ClassicWB-OS39
  IF EXISTS SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info
    makeicon >NIL: SYSTEMDIR:Locale/Help/ClassicWB-OS39 FROM SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info 
  ELSE
    makeicon >NIL: SYSTEMDIR:Locale/Help/ClassicWB-OS39
  ENDIF
ENDIF
IF NOT EXISTS SYSTEMDIR:Locale/Help.info
  IF EXISTS SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info
    makeicon >NIL: SYSTEMDIR:Locale/Help FROM SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info 
  ELSE
    makeicon >NIL: SYSTEMDIR:Locale/Help
  ENDIF
ENDIF
IF NOT EXISTS SYSTEMDIR:Locale.info
  IF EXISTS SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info
    makeicon >NIL: SYSTEMDIR:Locale FROM SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info 
  ELSE
    makeicon >NIL: SYSTEMDIR:Locale
  ENDIF
ENDIF
copy >NIL: PACKAGEDIR:README SYSTEMDIR:Locale/Help/ClassicWB-OS39
copy >NIL: PACKAGEDIR:README.guide SYSTEMDIR:Locale/Help/ClassicWB-OS39
copy >NIL: PACKAGEDIR:README.info SYSTEMDIR:Locale/Help/ClassicWB-OS39
IF NOT EXISTS SYSTEMDIR:Locale/Help/ClassicWB-OS39/screenshots
  makepath >NIL: SYSTEMDIR:Locale/Help/ClassicWB-OS39/screenshots
ENDIF
copy >NIL: PACKAGEDIR:screenshots/#?.iff SYSTEMDIR:Locale/Help/ClassicWB-OS39/screenshots


; detect amiga os version
set amigaosversion "3.1"
IF EXISTS "SYSTEMDIR:Libs/workbench.library"
  ; set amiga os version to 3.9, if workbench.library file is equal or greater than v45.102
  Version >NIL: "SYSTEMDIR:Libs/workbench.library" 45 102 FILE
  IF $RC EQ 0 VAL
    set amigaosversion "3.9"
  ENDIF

  ; set amiga os version to 3.9, if workbench.library file is equal or greater than v45.127
  Version >NIL: "SYSTEMDIR:Libs/workbench.library" 45 127 FILE
  IF $RC EQ 0 VAL
    set amigaosversion "3.9"
  ENDIF

  ; set amiga os version to 3.1.4, if workbench.library file is equal or greater than v45.194
  Version >NIL: "SYSTEMDIR:Libs/workbench.library" 45 194 FILE
  IF $RC EQ 0 VAL
    set amigaosversion "3.1.4"
  ENDIF

  ; set amiga os version to 3.2, if workbench.library file is equal or greater than v47.33
  Version >NIL: "SYSTEMDIR:Libs/workbench.library" 47 33 FILE
  IF $RC EQ 0 VAL
    set amigaosversion "3.2"
  ENDIF
ELSE
  ; set amiga os version to 3.9, if loaded workbench.library is equal or greater than v45.102
  Version >NIL: "workbench.library" 45 102
  IF $RC EQ 0 VAL
    set amigaosversion "3.9"
  ENDIF

  ; set amiga os version to 3.9, if loaded workbench.library is equal or greater than v45.127
  Version >NIL: "workbench.library" 45 127
  IF $RC EQ 0 VAL
    set amigaosversion "3.9"
  ENDIF

  ; set amiga os version to 3.1.4, if loaded workbench.library is equal or greater than v45.194
  Version >NIL: "workbench.library" 45 194
  IF $RC EQ 0 VAL
    set amigaosversion "3.1.4"
  ENDIF

  ; set amiga os version to 3.2, if loaded workbench.library is equal or greater than v47.33
  Version >NIL: "workbench.library" 47 33
  IF $RC EQ 0 VAL
    set amigaosversion "3.2"
  ENDIF
ENDIF


; Extract classicwb os39 v28 zip to systemdir
echo "Installing ClassicWB OS39..." 

; create backup of startup sequence
IF EXISTS SYSTEMDIR:S/Startup-Sequence
  Copy >NIL: SYSTEMDIR:S/Startup-Sequence SYSTEMDIR:S/Startup-Sequence.BAK
ENDIF

; create backup of user startup
IF EXISTS SYSTEMDIR:S/User-Startup
  Copy >NIL: SYSTEMDIR:S/User-Startup SYSTEMDIR:S/User-Startup.BAK
ENDIF

IF NOT "$amigaosversion" EQ "3.9"
  ; backup amiga os files
  MakePath >NIL: SYSTEMDIR:Temp/_Backup
  Copy >NIL: SYSTEMDIR:C SYSTEMDIR:Temp/_Backup/C ALL CLONE
  Copy >NIL: SYSTEMDIR:Classes SYSTEMDIR:Temp/_Backup/Classes ALL CLONE
  Copy >NIL: SYSTEMDIR:L SYSTEMDIR:Temp/_Backup/L ALL CLONE
  Copy >NIL: SYSTEMDIR:Devs SYSTEMDIR:Temp/_Backup/Devs ALL CLONE
  Copy >NIL: SYSTEMDIR:Libs SYSTEMDIR:Temp/_Backup/Libs ALL CLONE 

  ; extract classicwb os39 excluding files conflicting with amiga os 3.2, 3.1.4 and 3.1
  unzip -qq -o -x PACKAGEDIR:classicwb_os39_v28.zip -d SYSTEMDIR: -x *LoadModule *SetPatch *FastFileSystem *68040.library *lowlevel.library

  ; restore amiga os files
  Copy >NIL: SYSTEMDIR:Temp/_Backup/#? SYSTEMDIR: ALL CLONE
  Delete >NIL: SYSTEMDIR:Temp/_Backup ALL

  ; delete icons for programs not present with amiga os 3.2 and 3.1.4, 3.1
  Delete >NIL: SYSTEMDIR:WBStartup/AmiDock.info
  Delete >NIL: SYSTEMDIR:WBStartup/ASyncWB.info
  Delete >NIL: SYSTEMDIR:WBStartup/BenchTrash.info
  Delete >NIL: SYSTEMDIR:WBStartup/DefIcons.info
  Delete >NIL: SYSTEMDIR:WBStartup/RAWBInfo.info
  Delete >NIL: SYSTEMDIR:Prefs/ViNCEd.info
  Delete >NIL: SYSTEMDIR:System/Shell_VNC.info

  ; reinstall mui to fix igame
  MakePath >NIL: "SYSTEMDIR:Temp/MUI"
  lha -m1 -q x "PACKAGEDIR:Patches/mui38usr.lha" "SYSTEMDIR:Temp/MUI/"
  Copy >NIL: "SYSTEMDIR:Temp/MUI/MUI/#?" "SYSTEMDIR:System/MUI/" CLONE ALL
  Delete >NIL: "SYSTEMDIR:Temp/MUI/" ALL

  ; copy amiga os 3.2, 3.1.4 and 3.1 patches to systemdir
  Copy >NIL: PACKAGEDIR:Patches/SystemDir/#? SYSTEMDIR: ALL

  ; copy amiga os 3.1 patches to systemdir, if amiga os 3.1
  IF "$amigaosversion" EQ "3.1"
    Copy >NIL: PACKAGEDIR:Patches/Amiga-OS-3.1/#? SYSTEMDIR: ALL
  ENDIF

  ; delete pipe dosdrivers, if amiga os 3.2
  IF "$amigaosversion" EQ "3.2"
    Delete >NIL: SYSTEMDIR:Devs/DOSDrivers/PIPE#?

  ENDIF
ELSE
  ; extract classicwb os39
  unzip -qq -o -x PACKAGEDIR:classicwb_os39_v28.zip -d SYSTEMDIR:
ENDIF

; disable newicons, if amiga os is not 3.1
If Not "$amigaosversion" EQ "3.1"
  If Exists SYSTEMDIR:C/NewIcons
    Rename >NIL: SYSTEMDIR:C/NewIcons SYSTEMDIR:C/NewIcons_disabled
  EndIf
EndIf

; backup classicwb os39 startup sequence
Copy >NIL: SYSTEMDIR:T/Science SYSTEMDIR:S/Startup-Sequence.CWB
Delete >NIL: SYSTEMDIR:T/Science

; backup classicwb os39 user startup
Copy >NIL: SYSTEMDIR:S/User-Startup SYSTEMDIR:S/User-Startup.CWB

; restore startup sequence
Copy >NIL: SYSTEMDIR:S/Startup-Sequence.BAK SYSTEMDIR:S/Startup-Sequence

; restore user startup, if it exists. otherwise create empty user startup
If Exists SYSTEMDIR:S/User-Startup.BAK
  Copy >NIL: SYSTEMDIR:S/User-Startup.BAK SYSTEMDIR:S/User-Startup
Else
  Echo "" NOLINE >SYSTEMDIR:S/User-Startup
EndIf

; copy classicwb patches to systemdir
Copy >NIL: PACKAGEDIR:Patches/ClassicWB/#? SYSTEMDIR: ALL CLONE

; create boot startup, if it doesn't exist
If Not Exists SYSTEMDIR:S/Boot-Startup
  Echo "" NOLINE >SYSTEMDIR:S/Boot-Startup
EndIf

; add iconlib startup, if amiga os 3.1
IF "$amigaosversion" EQ "3.1"
  ; add iconlib startup to boot startup, if not present
  Search >NIL: SYSTEMDIR:S/Boot-Startup "Execute S:IconLib-Startup" quiet
  IF NOT $RC EQ 0 VAL
    Type PACKAGEDIR:Patches/Add-IconLib-Startup >>SYSTEMDIR:S/Boot-Startup
  ENDIF
ENDIF

; find set patch in startup sequence
Search SYSTEMDIR:S/Startup-Sequence "SetPatch" NONUM >T:_setpatch1
IF $RC EQ 0 VAL
  ; add boot startup to startup sequence after "SetPatch", if not present 
  Search >NIL: SYSTEMDIR:S/Startup-Sequence "Execute S:Boot-Startup" quiet
  IF NOT $RC EQ 0 VAL
    ; get found set patch text
    sed "1q;d" T:_setpatch1 >T:_setpatch2
    set setpatch `type T:_setpatch2`

    rep SYSTEMDIR:S/Startup-Sequence "$setpatch" "If Exists S:Boot-Startup*N  Execute S:Boot-Startup*NEndIf*N$setpatch"
  ENDIF

  ; disable startup sequence setpatch for amiga os 3.9
  IF "$amigaosversion" EQ "3.9"
      rep SYSTEMDIR:S/Startup-Sequence "$setpatch" ";$setpatch"
  ENDIF
ELSE
  echo "*e[1mError: Unable to add boot startup,*N'SetPatch'*Nnot found in startup sequence.*NBoot startup can't be installed without this!*e[0m"
  ask "Press ENTER to continue"
ENDIF
Delete >NIL: T:_setpatch#?

IF "$amigaosversion" EQ "3.9"
  ; add setpatch startup to boot startup, if not present
  Search >NIL: SYSTEMDIR:S/Boot-Startup "Execute S:SetPatch-Startup" quiet
  IF NOT $RC EQ 0 VAL
    Type PACKAGEDIR:Patches/Add-SetPatch-Startup >>SYSTEMDIR:S/Boot-Startup
  ENDIF
EndIf

; add boot menu startup to startup sequence after "S:Boot-Startup", if not present
Search >NIL: SYSTEMDIR:S/Startup-Sequence "S:BootMenu-Startup" quiet
IF NOT $RC EQ 0 VAL
  rep SYSTEMDIR:S/Startup-Sequence "Execute S:Boot-Startup" "Execute S:Boot-Startup*NEndIf*NIF EXISTS S:BootMenu-Startup*N  Execute S:BootMenu-Startup"
ENDIF

; find bind drivers in startup sequence
Search SYSTEMDIR:S/Startup-Sequence "BindDrivers" NONUM >T:_binddrivers1
IF $RC EQ 0 VAL
  ; add boot patches startup to startup sequence after "BindDrivers", if not present
  Search >NIL: SYSTEMDIR:S/Startup-Sequence "S:BootPatches-Startup" quiet
  IF NOT $RC EQ 0 VAL
    ; get found binddrivers text
    sed "1q;d" T:_binddrivers1 >T:_binddrivers2
    set binddrivers `sed "1q;d" T:_binddrivers2`

    rep SYSTEMDIR:S/Startup-Sequence "$binddrivers" "IF EXISTS S:BootPatches-Startup*N  Execute S:BootPatches-Startup*NEndIf*N$binddrivers"
  ENDIF
ELSE
  echo "*e[1mError: Unable to add boot patches startup,*N'BindDrivers'*Nnot found in startup sequence.*NBoot patches startup can't be installed without this!*e[0m"
  ask "Press ENTER to continue"
ENDIF
Delete >NIL: T:_binddrivers#?

; add workbench patches startup to user startup, if not present
Search >NIL: SYSTEMDIR:S/User-Startup "Execute S:WorkbenchPatches-Startup" quiet
IF NOT $RC EQ 0 VAL
  Type PACKAGEDIR:Patches/Add-WorkbenchPatches >>SYSTEMDIR:S/User-Startup
ENDIF

; set harddisk icon
set harddiskicon ""
IF EXISTS "SYSTEMDIR:Prefs/Env-Archive/Sys/def_disk.info"
  set harddiskicon "SYSTEMDIR:Prefs/Env-Archive/Sys/def_disk.info"
ELSE
  IF EXISTS "SYSTEMDIR:Disk.info"
    set harddiskicon "SYSTEMDIR:Disk.info"
  ENDIF
ENDIF

; update disk icon, if harddisk icon exists
IF NOT "$harddiskicon" EQ ""
  ; update dh1 disk icon, if dh1: device exists
  Assign >NIL: EXISTS DH1:
  IF $RC EQ 0 VAL
    Copy >NIL: "$harddiskicon" "DH1:Disk.info"
  ENDIF

  ; update dh2 disk icon, if dh2: device exists
  Assign >NIL: EXISTS DH2:
  IF $RC EQ 0 VAL
    Copy >NIL: "$harddiskicon" "DH2:Disk.info"
  ENDIF
ENDIF


;execute DH1:s/Activate
assign FONTS: SYSTEMDIR:Fonts
protect FONTS:#? +RWED ALL QUIET
SYSTEMDIR:System/FixFonts
assign FONTS: SYS:Fonts
delete >NIL: SYSTEMDIR:s/Activate

delete >NIL: SYSTEMDIR:.backdrop
rename >NIL: SYSTEMDIR:.backdropT SYSTEMDIR:.backdrop
delete >NIL: SYSTEMDIR:backdropT
delete >NIL: SYSTEMDIR:Install_CWB39
delete >NIL: SYSTEMDIR:Install_CWB39.info 


SETENV TZ MET-1MEST,M3.5.0,M10.5.0/03

LAB IGAME

IF EXISTS SYSTEMDIR:Temp/igame.zip
  cls
  echo ""
  echo ""
  echo ""
  echo ""
  echo ""
  echo ""
  echo "iGame Artwork:"
  echo "--------------"
  echo ""
  echo "The packs come with a list launcher and Zetro's low mem"
  echo "AGLaunch for WHDLoad Games/Demos and Other programs."
  echo ""
  echo "iGame is also available for WHDLoad titles."
  echo ""
  echo "Artwork can be installed for iGame, at the expense of"
  echo "approximately 20MB of hard drive space."
ELSE
  SKIP PALETTE
EndIF

SYSTEMDIR:C/Requestchoice >ENV:Choice "iGame Artwork""Choose:""Install""Remove"

IF $Choice EQ "1"
  SKIP INSTALL
ENDIF
IF $Choice EQ "0"
  SKIP REMOVE
ENDIF

LAB INSTALL

  cls
  echo ""
  echo "Installing... this will take some time!"
  echo ""
  unzip >NIL: -o SYSTEMDIR:Temp/igame.zip -d SYSTEMDIR:Games
  echo ""
  echo "iGame support installed."
  echo ""
  delete >NIL: SYSTEMDIR:Temp/igame.zip
  wait 1
  SKIP PALETTE

LAB REMOVE

  delete >NIL: SYSTEMDIR:Temp/igame.zip

LAB PALETTE

IF EXISTS SYSTEMDIR:Temp/palette.zip
  cls
  echo ""
  echo "ClassicWB can run in faster but limited 16 colour screenmode"
  echo "or with Higher Colours. For 16 colours choose a palette:"
  echo "" 
  echo "ClassicWB default Palette"
  echo "-------------------------"
  echo "The packs come with a default fixed 16 colour Palette"
  echo "based on Rebel's and Cammy's work. This Palette has been"
  echo "created to keep the original ClassicWB theme intact whilst"
  echo "maintaining excellent performance for applications, icons"
  echo "and background wallpapers."
  echo "Enable this palette if you want the same Workbench shown on"
  echo "ClassicWB webpage - abime.classicwb.net."
  echo ""
  echo "Rebel's optimized Palette"
  echo "-------------------------"
  echo "Rebel's original Palette is available, offering the very best"
  echo "16 colour palette for general use. Window borders and icons"
  echo "will not adhere to the original ClassicWB theme, using custom"
  echo "colours, however everything will be interlaced/MagicTV"
  echo "friendly and 16 colour images will look optimal."
  echo "Choose this theme if you intend customizing Workbench's look,"
  echo "run in interlaced or using Rebel's and Cammy's icons,"
  echo "borders and wallpapers."
ELSE
  SKIP THEME
EndIF

SYSTEMDIR:C/Requestchoice >ENV:Choice "Choose Palette""Choose:""Default 16""Optimized 16""Higher Colours"

IF $Choice EQ "1"
  SKIP DEFAULT
ENDIF
IF $Choice EQ "2"
  SKIP OPTIMIZED
ENDIF
IF $Choice EQ "0"
  SKIP HICOLOUR
ENDIF

LAB DEFAULT

  delete >NIL: SYSTEMDIR:Temp/palette.zip
  delete >NIL: SYSTEMDIR:Temp/hicolour.zip
  SETENV default 1
  SKIP THEME

LAB OPTIMIZED

  cls
  echo ""
  echo "Installing..."
  echo ""
  unzip >NIL: -o SYSTEMDIR:Temp/palette.zip -d SYSTEMDIR:
  echo ""
  echo "Optimized Palette installed."
  echo ""
  delete >NIL: SYSTEMDIR:Temp/hicolour.zip
  delete >NIL: SYSTEMDIR:Temp/palette.zip
  SETENV default 2
  wait 1
  SKIP THEME

LAB HICOLOUR

  cls
  echo ""
  echo "Installing..."
  echo ""
  unzip >NIL: -o SYSTEMDIR:Temp/hicolour.zip -d SYSTEMDIR:
  echo ""
  echo "High Colour Palette installed."
  echo ""
  delete >NIL: SYSTEMDIR:Temp/palette.zip
  delete >NIL: SYSTEMDIR:Temp/hicolour.zip
  SETENV default 1
  wait 1

LAB THEME

IF EXISTS SYSTEMDIR:Temp/retro.zip
  cls
  echo ""
  echo ""
  echo ""
  echo ""
  echo ""
  echo ""
  echo "Choose Theme:"
  echo "-------------"
  echo ""
  echo "None/Original Theme"
  echo "-------------------"
  echo "- Default Workbench appearance"
  echo "- No wallpaper"
  echo "- Copper effect applied to backdrop"
  echo "- Same as shown on main Webpage - abime.classicwb.net"
  echo ""
  echo "Retro and Re-Gen Theme"
  echo "----------------------"
  echo "- Customized appearance"
  echo "- Wallpaper included"
  echo "- Outlined Fonts"
  echo "- Copper effect applied to windows and applications"
ELSE
  SKIP COPPER
EndIF

SYSTEMDIR:C/Requestchoice >ENV:Choice "Choose Theme""Choose:""None""Retro""Re-Gen"

IF $Choice EQ "1"
  SKIP NONE
ENDIF
IF $Choice EQ "2"
  SKIP RETRO
ENDIF
IF $Choice EQ "0"
  SKIP REGEN
ENDIF

LAB NONE

  delete >NIL: SYSTEMDIR:Temp/retro.zip
  delete >NIL: SYSTEMDIR:Temp/regen.zip
  delete >NIL: SYSTEMDIR:Temp/default.zip
  delete >NIL: SYSTEMDIR:Temp/optimized.zip
  SKIP COPPER

LAB RETRO

  cls
  echo ""
  echo "Installing..."
  echo ""
  unzip >NIL: -o SYSTEMDIR:Temp/retro.zip -d SYSTEMDIR:
  echo ""
  echo "Retro Theme installed."
  echo ""
  IF $default EQ 1
    unzip >NIL: -o SYSTEMDIR:Temp/default.zip -d SYSTEMDIR:
  EndIF
  IF $default EQ 2
    unzip >NIL: -o SYSTEMDIR:Temp/optimized.zip -d SYSTEMDIR:
  EndIF
  delete >NIL: SYSTEMDIR:Temp/retro.zip
  delete >NIL: SYSTEMDIR:Temp/regen.zip
  delete >NIL: SYSTEMDIR:Temp/default.zip
  delete >NIL: SYSTEMDIR:Temp/optimized.zip
  wait 1
  SKIP COPPER

LAB REGEN

  cls
  echo ""
  echo "Installing..."
  echo ""
  unzip >NIL: -o SYSTEMDIR:Temp/regen.zip -d SYSTEMDIR:
  echo ""
  echo "Re-Gen Theme installed."
  echo "" 
  IF $default EQ 1
    unzip >NIL: -o SYSTEMDIR:Temp/default.zip -d SYSTEMDIR:
  EndIF
  IF $default EQ 2
    unzip >NIL: -o SYSTEMDIR:Temp/optimized.zip -d SYSTEMDIR:
  EndIF
  delete >NIL: SYSTEMDIR:Temp/retro.zip
  delete >NIL: SYSTEMDIR:Temp/regen.zip
  delete >NIL: SYSTEMDIR:Temp/default.zip
  delete >NIL: SYSTEMDIR:Temp/optimized.zip
  wait 1

LAB COPPER

; disable copper, border blank, stack attack and skip to anticlick, if amiga os is not 3.9
IF NOT "$amigaosversion" EQ "3.9"
  delete >NIL: SYSTEMDIR:Temp/Copper_enable
  delete >NIL: SYSTEMDIR:Temp/BorderBlank_enable
  delete >NIL: SYSTEMDIR:Temp/StackAttack_enable
  SKIP ANTICLICK
ENDIF 

IF EXISTS SYSTEMDIR:C/Copper_enable
  cls
  echo ""
  echo ""
  echo ""
  echo ""
  echo ""
  echo ""
  echo "AGA Copper backgrounds"
  echo "----------------------"
  echo ""
  echo "AGA users are treated with the ability to run a high"
  echo "colour Copper background."
  echo ""
  echo "If you chose the ClassicWB default Theme, it will be"
  echo "used as the background wallpaper."
  echo ""
  echo "Users who selected the custom Themes with wallpaper"
  echo "will have the Copper effect applied to the background"
  echo "of windows and applications."
  echo ""
  echo "This can be Enabled or Disabled at anytime from the"
  echo "Settings and Tools menus. The effect can be locked to"
  echo "any one colour using preferences."
  echo ""
  echo "ECS users need to disable this option."
ELSE
  SKIP BORDER
EndIF

SYSTEMDIR:C/Requestchoice >ENV:Choice "Copper Background""Choose:""Enable""Disable"

IF $Choice EQ "1"
  SKIP ENABLE
ENDIF
IF $Choice EQ "0"
  SKIP DISABLE
ENDIF

LAB ENABLE

  cls
  echo ""
  echo "Installing..."
  echo ""
  execute >NIL: PACKAGEDIR:Temp/Copper_enable
  echo ""
  echo "Copper support installed."
  echo ""
  delete >NIL: SYSTEMDIR:Temp/Copper_enable
  delete >NIL: SYSTEMDIR:Temp/BorderBlank_enable
  wait 1
  SKIP BORDER

LAB DISABLE

  delete >NIL: SYSTEMDIR:Temp/Copper_enable

LAB BORDER

IF EXISTS SYSTEMDIR:Temp/BorderBlank_enable
  cls
  echo ""
  echo ""
  echo ""
  echo ""
  echo ""
  echo ""
  echo "Black Border:"
  echo "-------------"
  echo ""
  echo "Workbench normally has a matching border surround. This"
  echo "can be changed to a black border."
  echo ""
  echo "This option can be enabled/disabled at anytime via the"
  echo "Settings->Components menu."
ELSE
  SKIP STACK
EndIF

SYSTEMDIR:C/Requestchoice >ENV:Choice "BlackBorder""Choose:""Enable""Disable"

IF $Choice EQ "1"
  SKIP ENABLE
ENDIF
IF $Choice EQ "0"
  SKIP DISABLE
ENDIF

LAB ENABLE

  cls
  echo ""
  echo "Installing..."
  echo ""
  execute PACKAGEDIR:Temp/BorderBlank_enable
  echo ""
  echo "Black Border support installed."
  echo ""
  delete >NIL: SYSTEMDIR:Temp/BorderBlank_enable
  wait 1
  SKIP STACK

LAB DISABLE

  delete >NIL: SYSTEMDIR:Temp/BorderBlank_enable

LAB STACK

; disable stack attack and skip to anticlick, if amiga os is not 3.9
IF NOT "$amigaosversion" EQ "3.9"
  delete >NIL: SYSTEMDIR:Temp/StackAttack_enable
  SKIP ANTICLICK
ENDIF

IF EXISTS SYSTEMDIR:Temp/StackAttack_enable
  cls
  echo ""
  echo ""
  echo ""
  echo ""
  echo ""
  echo ""
  echo "StackAttack:"
  echo "------------"
  echo ""
  echo "ClassicWB already has stack settings that are larger"
  echo "than default in certain cases to improve stability."
  echo ""
  echo "StackAttack makes sure that all processes are launched"
  echo "with a pre-defined minimum stack to improve stability"
  echo "in certain situations, with the downside of using a"
  echo "little extra RAM."
  echo ""
  echo "This option can be enabled/disabled at anytime via the"
  echo "Settings->Components menu."
ELSE
  SKIP ANTICLICK
EndIF

SYSTEMDIR:C/Requestchoice >ENV:Choice "StackAttack""Choose:""Enable""Disable"

IF $Choice EQ "1"
  SKIP ENABLE
ENDIF
IF $Choice EQ "0"
  SKIP DISABLE
ENDIF

LAB ENABLE

  cls
  echo ""
  echo "Installing..."
  echo ""
  execute PACKAGEDIR:Temp/StackAttack_enable
  echo ""
  echo "StackAttack support installed."
  echo ""
  delete >NIL: SYSTEMDIR:Temp/StackAttack_enable
  wait 1
  SKIP ANTICLICK

LAB DISABLE

  delete >NIL: SYSTEMDIR:Temp/StackAttack_enable
 
LAB ANTICLICK

IF EXISTS SYSTEMDIR:Temp/AntiClick_disable
  cls
  echo ""
  echo ""
  echo ""
  echo ""
  echo ""
  echo ""
  echo "AntiClick"
  echo "---------"
  echo ""
  echo "Constant floppy drive clicking can either be nostalgic"
  echo "or annoying. This patch prevents the constant clicking"
  echo "in Workbench."
  echo ""
  echo "If you drive continues to click after enabling this"
  echo "patch, please disable it immediately as your drive is"
  echo "incompatible."
  echo ""
  echo "This option can be enabled/disabled at anytime via the"
  echo "Settings->Components menu." 
ELSE
  SKIP CYBERBUG
EndIF

SYSTEMDIR:C/Requestchoice >ENV:Choice "AntiClick""Choose:""Enable""Disable"

IF $Choice EQ "1"
  SKIP ENABLE
ENDIF
IF $Choice EQ "0"
  SKIP DISABLE
ENDIF

LAB ENABLE

  delete >NIL: SYSTEMDIR:Temp/AntiClick_disable
  SKIP CYBERBUG

LAB DISABLE

  cls
  echo ""
  echo "Removing..."
  echo ""
  execute PACKAGEDIR:Temp/AntiClick_disable
  echo ""
  echo "AntiClick support removed."
  echo ""
  delete >NIL: SYSTEMDIR:Temp/AntiClick_disable
  wait 1
  
LAB CYBERBUG

; disable cyberbug fix and skip to freewheel, if amiga os is not 3.9
IF NOT "$amigaosversion" EQ "3.9"
  delete >NIL: SYSTEMDIR:Temp/CyberBugFix_disable
  SKIP FREEWHEEL
ENDIF

IF EXISTS SYSTEMDIR:Temp/CyberBugFix_disable
  cls
  echo ""
  echo ""
  echo ""
  echo ""
  echo ""
  echo ""
  echo "CyberBugFix"
  echo "-----------"
  echo ""
  echo "CyberBugFix is a patch that saves an extra chipram when"
  echo "enabled."
  echo ""
  echo "Its known to cause problems with Novacoders ScummVM port,"
  echo "so if you intend to run ScummVM please remember to disable"
  echo "this patch first."
  echo ""
  echo "This option can be enabled/disabled at anytime via the"
  echo "Settings->Components menu." 
ELSE
  SKIP FREEWHEEL
EndIF

SYSTEMDIR:C/Requestchoice >ENV:Choice "CyberBugFix""Choose:""Enable""Disable"

IF $Choice EQ "1"
  SKIP ENABLE
ENDIF
IF $Choice EQ "0"
  SKIP DISABLE
ENDIF

LAB ENABLE

  delete >NIL: SYSTEMDIR:Temp/CyberBugFix_disable
  SKIP FREEWHEEL

LAB DISABLE

  cls
  echo ""
  echo "Removing..."
  echo ""
  execute PACKAGEDIR:Temp/CyberBugFix_disable
  echo ""
  echo "CyberBugFix support removed."
  echo ""
  wait 1

LAB FREEWHEEL

IF EXISTS SYSTEMDIR:Temp/FreeWheel_disable
  cls
  echo ""
  echo ""
  echo ""
  echo ""
  echo ""
  echo ""
  echo "FreeWheel"
  echo "---------"
  echo ""
  echo "FreeWheel allows you to scroll windows using a middle"
  echo "mouse button and map functions to a scroll wheel mouse."
  echo ""
  echo "It also provides Click-To-Front on windows so you dont"
  echo "have to search for the depth gadget."
  echo ""
  echo "It is not a serial mouse driver however such as NewMouse."
  echo ""
  echo "This option can be enabled/disabled at anytime via the"
  echo "Settings->Components menu."
ELSE
  SKIP MYCLOCK
EndIF

SYSTEMDIR:C/Requestchoice >ENV:Choice "FreeWheel""Choose:""Enable""Disable"

IF $Choice EQ "1"
  SKIP ENABLE
ENDIF
IF $Choice EQ "0"
  SKIP DISABLE
ENDIF

LAB ENABLE

  delete >NIL: SYSTEMDIR:Temp/FreeWheel_disable
  SKIP MYCLOCK

LAB DISABLE

  cls
  echo ""
  echo "Removing..."
  echo ""
  execute PACKAGEDIR:Temp/FreeWheel_disable
  echo ""
  echo "FreeWheel support removed."
  echo ""
  wait 1

LAB MYCLOCK

; disable MyClock and skip to complete, if amiga os is not 3.9
IF NOT "$amigaosversion" EQ "3.9"
  IF EXISTS SYSTEMDIR:Temp/MyClock_disable
    execute PACKAGEDIR:Temp/MyClock_disable
    delete >NIL: SYSTEMDIR:Temp/MyClock_disable
  ENDIF
  SKIP complete
ENDIF

IF EXISTS SYSTEMDIR:Temp/MyClock_disable
  cls
  echo ""
  echo ""
  echo ""
  echo ""
  echo ""
  echo ""
  echo "MyClock"
  echo "-------"
  echo ""
  echo "MyClock is a Workbench Titlebar Clock ultility which"
  echo "will place the current date and time in the top right"
  echo "hand corner of the screen."
  echo ""
  echo "When changing screenmodes, it will need to be quit"
  echo "first, which can be easily achieved by right clicking"
  echo "on the clock itself."
  echo ""
  echo "TinyMeter also has a clock but can sometimes freeze on"
  echo "boot in certain situations, therefore this utility can"
  echo "be used instead."
  echo ""
  echo "This option can be enabled/disabled at anytime via the"
  echo "Settings->Components menu."
ELSE
  SKIP complete
EndIF

SYSTEMDIR:C/Requestchoice >ENV:Choice "MyClock""Choose:""Enable""Disable"

IF $Choice EQ "1"
  SKIP ENABLE
ENDIF
IF $Choice EQ "0"
  SKIP DISABLE
ENDIF

LAB ENABLE

  delete >NIL: SYSTEMDIR:Temp/MyClock_disable
  SKIP complete

LAB DISABLE

  cls
  echo ""
  echo "Removing..."
  echo ""
  execute PACKAGEDIR:Temp/MyClock_disable
  echo ""
  echo "MyClock support removed."
  echo ""
  delete >NIL: SYSTEMDIR:Temp/MyClock_disable
  wait 1


; Complete
; --------
LAB complete

cls

echo ""
echo "Installation Complete!"
echo ""
echo "Check the website documentation for details of the"
echo "multiboot and multi-partition features, large HD >4GB"
echo "support and how to add WHDLoad games."
echo ""
echo "The MyFiles/Install drawer contains many useful addons"
echo "including Network, CDROM drivers and more."
echo ""
echo "Also check out the Settings->Components menu to access"
echo "more options like ENV Handler, BlazeWPC, IconBeFast,"
echo "TinyMeter, TagLiFE, PatchRAM, PeterKs PNG Icon Support"
echo "and VisualPrefs."


; End
; ---
LAB end

echo ""
ask "Press RETURN to continue."
cls